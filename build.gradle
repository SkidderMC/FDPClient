plugins {
    id "java"
    id "idea"
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge"
    id "org.spongepowered.mixin"
    id "com.gorylenko.gradle-git-properties" version "2.5.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += ["-Dfml.coreMods.load=net.ccbluex.liquidbounce.injection.forge.MixinLoader", "-Xmx4096m", "-Xms1024m", "-Ddev-mode"]

    def spotifyClientId = (project.findProperty("spotify_client_id") ?: "").toString()
    def spotifyClientSecret = (project.findProperty("spotify_client_secret") ?: "").toString()
    def spotifyRefreshToken = (project.findProperty("spotify_refresh_token") ?: "").toString()
    def spotifyPollInterval = (project.findProperty("spotify_poll_interval_seconds") ?: "5").toString()
    def spotifyHttpTimeout = (project.findProperty("spotify_http_timeout_ms") ?: "12000").toString()
    def spotifyDashboardUrl = (project.findProperty("spotify_dashboard_url") ?: "https://developer.spotify.com/dashboard").toString()
    def spotifyAuthGuideUrl = (project.findProperty("spotify_authorization_guide_url") ?: "https://developer.spotify.com/documentation/web-api/tutorials/refreshing-tokens").toString()
    def spotifyAuthScopes = (project.findProperty("spotify_authorization_scopes") ?: "user-read-currently-playing user-read-playback-state").toString()
    def spotifyAuthRedirectPort = (project.findProperty("spotify_authorization_redirect_port") ?: "43791").toString()
    def spotifyAuthRedirectPath = (project.findProperty("spotify_authorization_redirect_path") ?: "/spotify-oauth-callback").toString()

    clientJvmArgs += [
            "-Dspotify.clientId=${spotifyClientId}",
            "-Dspotify.clientSecret=${spotifyClientSecret}",
            "-Dspotify.refreshToken=${spotifyRefreshToken}",
            "-Dspotify.pollIntervalSeconds=${spotifyPollInterval}",
            "-Dspotify.httpTimeoutMs=${spotifyHttpTimeout}",
            "-Dspotify.dashboardUrl=${spotifyDashboardUrl}",
            "-Dspotify.authorizationGuideUrl=${spotifyAuthGuideUrl}",
            "-Dspotify.authorizationScopes=${spotifyAuthScopes}",
            "-Dspotify.authorizationRedirectPort=${spotifyAuthRedirectPort}",
            "-Dspotify.authorizationRedirectPath=${spotifyAuthRedirectPath}",
    ]
}

configurations {

    external
    compile.extendsFrom(external)

    runtimeOnly.canBeResolved = true
}

dependencies {
    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
        exclude module: "slf4j-api"
    }

    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    implementation('com.github.half-cambodian-hacker-man:Koffee:d8cee73') {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'asm'
    }

    implementation "com.jagrosh:DiscordIPC:0.4"

    implementation("com.github.CCBlueX:Elixir:1.2.6") {
        exclude module: "kotlin-stdlib"
        exclude module: "authlib"
    }

    final axochat4jVersion = "0.3.0"
    implementation("com.github.MukjepScarlet.axochat4j:axochat4j-core:$axochat4jVersion")
    implementation("com.github.MukjepScarlet.axochat4j:axochat4j-codec-gson:$axochat4jVersion") {
        transitive = false
    }
    implementation("com.github.MukjepScarlet.axochat4j:axochat4j-client-netty:$axochat4jVersion") {
        transitive = false
    }

    implementation("com.github.UnlegitMC:Astar3d:bec2291cf2")
    implementation 'com.jhlabs:filters:2.0.235'

    implementation("org.knowm.xchart:xchart:3.8.8")

    // HTTP Client
    implementation("com.squareup.okhttp3:okhttp:5.0.0-alpha.16") {
        exclude module: "kotlin-stdlib"
    }

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version"

    // Swing LookAndFeel
    implementation "com.formdev:flatlaf:3.6.1"

    implementation fileTree(include: ["*.jar"], dir: "libs")
}

shadowJar {
    archiveClassifier.set("")
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "LICENSE.txt"

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"

    exclude "org/apache/log4j/**"
    exclude "org/apache/commons/**"
    exclude "org/junit/**"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    rename "(.+_at.cfg)", "META-INF/\$1"
}

tasks.register('moveResources') {
    doLast {
        copy {
            from "${buildDir}/resources/main"
            into "${buildDir}/classes/java"
        }
    }
    it.dependsOn(processResources)
}

classes.dependsOn(moveResources)

jar {
    manifest.attributes(
            "FMLCorePlugin": "net.ccbluex.liquidbounce.injection.forge.MixinLoader",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "fdpclient.forge.mixins.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLAT": "fdpclient_at.cfg",
            "Main-Class": "net.setup.FDPInstructionsKt",
    )

    enabled = false

    dependsOn("shadowJar")
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "fdpclient.mixins.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

configurations {
    compileClasspath {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
    include {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
}

tasks.reobfShadowJar.mustRunAfter("shadowJar")
tasks.reobfJar.dependsOn("shadowJar")

tasks.register('copyZipInclude', Copy) {
    from 'zip_include/'
    into 'build/libs/zip'
}

tasks.build.dependsOn("copyZipInclude")